# Class: prometheus::bind_exporter
#
# This module manages prometheus bind_exporter
#
# @param arch
#  Architecture (amd64 or i386)
#
# @param bin_dir
#  Directory where binaries are located
#
# @param config_mode
#  The permissions of the configuration files
#
# @param download_extension
#  Extension for the release binary archive
#
# @param download_url
#  Complete URL corresponding to the where the release binary archive can be downloaded
#
# @param download_url_base
#  Base URL for the binary archive
#
# @param extra_groups
#  Extra groups to add the binary user to
#
# @param extra_options
#  Extra options added to the startup command
# @param env_vars
#  The environment variable to pass to the daemon
# @param group
#  Group under which the binary is running
#
# @param init_style
#  Service startup scripts style (e.g. rc, upstart or systemd)
#
# @param install_method
#  Installation method: url or package (only url is supported currently)
#
# @param manage_group
#  Whether to create a group for or rely on external code for that
#
# @param manage_service
#  Should puppet manage the service? (default true)
#
# @param manage_user
#  Whether to create user or rely on external code for that
#
# @param os
#  Operating system (linux is the only one supported)
#
# @param package_ensure
#  If package, then use this for package ensure default 'latest'
#
# @param package_name
#  The binary package name - not available yet
#
# @param purge_config_dir
#  Purge config files no longer generated by Puppet
#
# @param restart_on_change
#  Should puppet restart the service on configuration change? (default true)
#
# @param service_enable
#  Whether to enable the service from puppet (default true)
#
# @param service_ensure
#  State ensured for the service (default 'running')
#
# @param service_name
#  Name of the node exporter service (default 'bind_exporter')
#
# @param user
#  User which runs the service
#
# @param version
#  The binary release version
#
# @param export_scrape_job
#   Whether to export a `prometheus::scrape_job` to puppetDB for
#   collecting on your prometheus server.
# @param scrape_job_name
#   The name of the scrape job. When configuring prometheus with this
#   puppet module, the jobs to be collected are configured with
#   `prometheus::collect_scrape_jobs`.
# @param scrape_port
#   The port to use in the scrape job.  This won't normally need to be
#   changed unless you run the exporter with a non-default port by
#   overriding `extra_options`.
# @param scrape_job_labels
#   Labels to configure on the scrape job. If not set, the
#   `prometheus::daemon` default (`{ 'alias' => $scrape_host }`) will
#   be used.
# @param proxy_server
#  Optional proxy server, with port number if needed. ie: https://example.com:8080
# @param proxy_type
#  Optional proxy server type (none|http|https|ftp)
class prometheus::bind_exporter (
  Optional[Stdlib::HTTPSUrl] $download_url                   = undef,
  Array[String[1]] $extra_groups                             = [],
  String[1] $group                                           = 'bind-exporter',
  String[1] $package_ensure                                  = 'latest',
  String[1] $package_name                                    = 'bind_exporter',
  String[1] $user                                            = 'bind-exporter',
  String[1] $version                                         = '0.4.0',
  Boolean $purge_config_dir                                  = true,
  Boolean $restart_on_change                                 = true,
  Boolean $service_enable                                    = true,
  String[1] $service_ensure                                  = 'running',
  String $service_name                                       = 'bind_exporter',
  Prometheus::Initstyle $init_style                          = $prometheus::init_style,
  Prometheus::Install $install_method                        = $prometheus::install_method,
  Boolean $manage_group                                      = true,
  Boolean $manage_service                                    = true,
  Boolean $manage_user                                       = true,
  String[1] $os                                              = $prometheus::os,
  Optional[String[1]] $extra_options                         = undef,
  Hash[String, Scalar] $env_vars                             = {},
  String $download_extension                                 = 'tar.gz',
  Stdlib::HTTPSUrl $download_url_base                        = 'https://github.com/prometheus-community/bind_exporter/releases',
  String[1] $config_mode                                     = $prometheus::config_mode,
  String[1] $arch                                            = $prometheus::real_arch,
  Stdlib::Absolutepath $bin_dir                              = $prometheus::bin_dir,
  Boolean $export_scrape_job                                 = false,
  Stdlib::Port $scrape_port                                  = 9119,
  String[1] $scrape_job_name                                 = 'bind',
  Optional[Hash] $scrape_job_labels                          = undef,
  Optional[String[1]] $proxy_server                          = undef,
  Optional[Enum['none', 'http', 'https', 'ftp']] $proxy_type = undef,
) inherits prometheus {
  #Please provide the download_url for versions < 0.9.0
  $real_download_url    = pick($download_url,"${download_url_base}/download/v${version}/${package_name}-${version}.${os}-${arch}.${download_extension}")
  $notify_service = $restart_on_change ? {
    true    => Service[$service_name],
    default => undef,
  }

  prometheus::daemon { $service_name:
    install_method     => $install_method,
    version            => $version,
    download_extension => $download_extension,
    os                 => $os,
    arch               => $arch,
    real_download_url  => $real_download_url,
    bin_dir            => $bin_dir,
    notify_service     => $notify_service,
    package_name       => $package_name,
    package_ensure     => $package_ensure,
    manage_user        => $manage_user,
    user               => $user,
    extra_groups       => $extra_groups,
    group              => $group,
    manage_group       => $manage_group,
    purge              => $purge_config_dir,
    options            => $extra_options,
    env_vars           => $env_vars,
    init_style         => $init_style,
    service_ensure     => $service_ensure,
    service_enable     => $service_enable,
    manage_service     => $manage_service,
    export_scrape_job  => $export_scrape_job,
    scrape_port        => $scrape_port,
    scrape_job_name    => $scrape_job_name,
    scrape_job_labels  => $scrape_job_labels,
    proxy_server       => $proxy_server,
    proxy_type         => $proxy_type,
  }
}
