<%- |
  Array[String]             $ipv6_listen_ip,
  String                    $ipv6_listen_options,
  Stdlib::Port              $ipv6_listen_port,
  Array[String]             $listen_ip,
  Optional[String]          $listen_options,
  Stdlib::Port              $listen_port,
  String                    $mailhost_append,
  String[1]                 $mailhost_common,
  String                    $mailhost_prepend,
  String[1]                 $mailhost_ssl_settings,
  String[1]                 $nginx_version,
  Enum['on', 'off', 'only'] $starttls,
| -%>
# MANAGED BY PUPPET
server {
<%= $mailhost_prepend -%>
<%- $listen_ip.each |$ip| { -%>
  listen                <%= $ip %>:<%= $listen_port %><% if $listen_options { %> <%= $listen_options %><% } %>;
<%- } -%>
<%- $ipv6_listen_ip.each |$ipv6| { -%>
  listen                [<%= $ipv6 %>]:<%= $ipv6_listen_port %> <% if $ipv6_listen_options { %><%= $ipv6_listen_options %><% } %>;
<%- } -%>
<%= $mailhost_common -%>

<%- if versioncmp($nginx_version, '1.15.0') < 0 { -%>
  ssl                   off;
<% } %>
  starttls              <%= $starttls %>;

<% if $starttls != 'off' { %>
<%= $mailhost_ssl_settings -%>
<%- } -%>
<%= $mailhost_append -%>
}
